group 'net.igsoft'
version '0.9.1'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'scala'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

sourceSets {
    example {
        java.srcDir(file('src/example/java'))
        scala.srcDir(file('src/example/scala'))
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    exampleImplementation.extendsFrom implementation
    exampleRuntimeOnly.extendsFrom runtimeOnly
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name "Simple Dependency Injection for Java"
                packaging 'jar'

                description 'Simple Dependency Injection framework for Java'
                url 'https://github.com/aartiPl/SDI'

                scm {
                    connection 'scm:git:git://https://github.com/aartiPl/SDI.git'
                    developerConnection 'scm:git:ssh:https://github.com/aartiPl/SDI.git'
                    url 'https://github.com/aartiPl/SDI/tree/master'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'aartiPl'
                        name 'Marcin Kuszczak'
                        email 'marcin.kuszczak.apps@gmail.com'
                    }
                }
            }
        }
    }
}

dependencies {
    implementation 'com.google.guava:guava:23.5-jre'
    implementation 'ch.qos.logback:logback-classic:1.2.3'

    exampleImplementation 'org.scala-lang:scala-library:2.12.6'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.2.0'
    testImplementation 'org.assertj:assertj-core:3.11.1'
    testImplementation 'com.openpojo:openpojo:0.8.10'
    testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.1.4'
}

test {
    useJUnitPlatform()

    reports {
        html.enabled = true
    }
}
